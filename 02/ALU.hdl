// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:
    Mux16 (a=x , b=false , sel=zx , out=nx1);
    Not16 (in=nx1 , out=nx2);
    Mux16 (a=nx1 , b=nx2 , sel=nx , out=nx3);

    Mux16 (a=y , b=false , sel=zy , out=ny1);
    Not16 (in=ny1 , out=ny2);
    Mux16 (a=ny1 , b=ny2 , sel=ny , out=ny3);

    And16 (a=nx3 , b=ny3 , out=nxy1); 
    Add16 (a=nx3 , b=ny3 , out=nxy2);

    Mux16 (a=nxy1 , b=nxy2 , sel=f , out=nxy3);
    Not16 (in=nxy3 , out=nxy4);

    Mux16 (a=nxy3 , b=nxy4 , sel=no , out=out , out[0..7]=out1 , out[8..15]=out2 , out[15]=ng);

    Or8Way (in=out1 , out=or1);
    Or8Way (in=out2 , out=or2);
    Or (a=or1 , b=or2 , out=or3);

    Not (in=or3 , out=zr);
}

// Circuit diagram : https://docs.google.com/drawings/d/15HkYCzHEdW_A-QT4_sj5NaSU_rMutw7oFpw0N0sghz0/edit